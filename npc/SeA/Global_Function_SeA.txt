//1:zeny
//2:points
//3:cash
function	script	Give_to_All	{
	set $@para1,atoi(@cmd_para$[0]);
	if(!existitem($@para1) && !($@para1&3)) end;
	set $@para2,atoi(@cmd_para$[1]);
	switch($@para1)
	{
		case 1: script4each "{ if( isloggedin(getcharid(3)) != 1 ) end; set ZENY, ZENY+$@para2; dispmes msg(176); }",0; break;
		case 2: script4each "{ if( isloggedin(getcharid(3)) != 1 ) end; atcommand \"@points \"+$@para2; }",0; break;
		case 3: script4each "{ if( isloggedin(getcharid(3)) != 1 ) end; atcommand \"@cash \"+$@para2; }",0; break;
		default: script4each "{ if( isloggedin(getcharid(3)) != 1 ) end; getitem $@para1,$@para2; dispmes msg(18); }",0; break;
	}
}

function	script	cmd_mapmove	{
	set .@m$,@cmd_para$[0];
	set .@x,atoi(@cmd_para$[1]);
	set .@y,atoi(@cmd_para$[2]);
	if( !existmap(.@m$) ) {
		dispmes msg(1);
		end;
	}
	if( !.@x && !.@y ) ; else
	if( checkcell(.@m$,.@x,.@y,cell_chknopass) ) {
		dispmes msg(2);
		set .@x,0;
		set .@y,0;
	}
	if( getmapflag(.@m$,mf_nowarpto) && getbattleflag("any_warp_GM_min_level") > getcmduserlv() ) {
		dispmes msg(247);
		end;
	}
	if( getmapflag(.@m$,mf_nowarp) && getbattleflag("any_warp_GM_min_level") > getcmduserlv() ) {
		dispmes msg(248);
		end;
	}
	if( warp(.@m$,.@x,.@y) ) {
		dispmes msg(1);
		end;
	}
	dispmes msg(0);	
}

function	script	cmd_jumpto	{
	set .@n$,@cmd_para$[0];
	set .@i,getcharid(3,.@n$);
	if( HP == 0 ) {
		dispmes "死亡时无法传送";
		end;
	}
	if( !isloggedin(.@i) ) {
		dispmes msg(3);
		end;
	}
	getmapxy .@m1$,.@x,.@y,0;
	if( getmapflag(.@m1$,mf_nowarp) && getbattleflag("any_warp_GM_min_level") > getcmduserlv() ) {
		dispmes msg(248);
		end;
	}
	set .@j,getcharid(3);
	attachrid .@i;
	set .@n$, strcharinfo(0);
	getmapxy .@m2$,.@x,.@y,0;
	attachrid .@j;
	if( getmapflag(.@m2$,mf_nowarpto) && getbattleflag("any_warp_GM_min_level") > getcmduserlv() ) {
		dispmes msg(247);
		end;
	}
	if( warp(.@m2$,.@x,.@y) ) {
		dispmes msg(1);
		end;
	}
	dispmes msg(4,.@n$);
}

function	script	cmd_jump	{
	set .@x,atoi(@cmd_para$[0]);
	set .@y,atoi(@cmd_para$[1]);
	if( HP == 0 ) {
		dispmes "死亡时无法传送";
		end;
	}
	getmapxy .@m$,.@i,.@j,0;
	if( !.@x && !.@y ) ; else
	if( checkcell(.@m$,.@x,.@y,cell_chknopass) ) {
		dispmes msg(2);
		set .@x,0;
		set .@y,0;
	}
	if( getmapflag(.@m$,mf_noteleport) && getbattleflag("any_warp_GM_min_level") > getcmduserlv() ) {
		dispmes msg(248);
		end;
	}
	if( warp(.@m$,.@x,.@y) ) {
		dispmes msg(1);
		end;
	}
	getmapxy .@m$,.@x,.@y,0;
	dispmes msg(5,.@x,.@y);
}

function	script	cmd_where	{
	set .@n$,@cmd_para$[0];
	set .@i,getcharid(3,.@n$);
	if( !isloggedin(.@i) ) {
		dispmes msg(3);
		end;
	}
	if( getbattleflag("hide_GM_session") && getcmduserlv() < getgmlevel(.@i) &&
		!(getbattleflag("who_display_aid") && getcmduserlv() >= getbattleflag("who_display_aid"))
	) {
		dispmes msg(3);
		end;
	}
	set .@j,getcharid(3);
	attachrid .@i;
	set .@n$, strcharinfo(0);
	getmapxy .@m$,.@x,.@y,0;
	attachrid .@j;
	dispmes .@n$+" "+.@m$+" "+.@x+" "+.@y;
}

function	script	cmd_load	{
	if( getmapflag(getsavepoint(0), mf_nowarpto) &&  getbattleflag("any_warp_GM_min_level") > getcmduserlv() ) {
		dispmes msg(249);
		end;
	}
	getmapxy .@m$,.@x,.@y,0;
	if ( getmapflag(.@m$, mf_nowarp) &&  getbattleflag("any_warp_GM_min_level") > getcmduserlv() ) {
		dispmes msg(248);
		end;
	}
	warp(getsavepoint(0),getsavepoint(1),getsavepoint(2));
	dispmes msg(7);
}

function	script	cmd_delitem	{
	set .@i, atoi(@cmd_para$[0]);
	set .@j, atoi(@cmd_para$[1]);
	if(.@j < 1) set .@j, 1; 

	if( existitem(.@i) )
	{
		set .@k, countitem(.@i);
		if( .@k > .@j ) set .@k, .@j;
		dispmes msg(113,.@k);
		delitem .@i, .@k;
		end;
	}
	else if( existitem(@cmd_para$[0]) )
	{
		set .@k, countitem(@cmd_para$[0]);
		if( .@k > .@j ) set .@k, .@j;
		dispmes msg(113,.@k);
		delitem @cmd_para$[0], .@k;
		end;
	}
	dispmes msg(19);
	end;
}

function	script	cmd_monster	{
	if( existmob(atoi(@cmd_para$[0])) ) 
		set .@id, atoi(@cmd_para$[0]); 
	else if( !set(.@id, existmob(@cmd_para$[0])) ) 
	{ 
		dispmes msg(40); 
		end; 
	} 

	if( .@id == 1288 )
	{ 
		dispmes msg(83); 
		end; 
	} 
	
	set .@id, .@id+getarg(0)*MAX_MOB_DB;

	set .@i,atoi(@cmd_para$[1]); 
	if(.@i < 1)
		set .@i, 1;
	if( set(.@j,getbattleflag("atcommand_spawn_quantity_limit")) && .@i > .@j ) 
		set .@i,.@j; 

	getmapxy .@m$,.@x,.@y,0;
	set .@j, sqrt(.@i);
	areamonster .@m$, .@x-.@j-2, .@y-.@j-2, .@x+.@j+2, .@y+.@j+2, "--ja--", .@id, .@i; 
	dispmes msg(39);
}

function	script	cmd_clone	{
	if( !isloggedin(set(.@id, atoi(@cmd_para$[0]))) ) 
	{
		if( !isloggedin(set(.@id, getcharid(3,@cmd_para$[0]))) )
		{
			dispmes msg(3);
			end;
		}
	}

	set .@id2, getcharid(3);
	attachrid .@id;
	set .@id3, getcharid(0);
	attachrid .@id2;
	if(getgmlevel(.@id) > getcmduserlv()) {
		dispmes msg(126);
		end;
	}

	if( set(.@flag,getarg(0)) == 2 )
	{
		if( set(.@j,getbattleflag("atcommand_slave_clone_limit")) && countslave(.@id2) >= .@j ) 
		{
			dispmes msg(127);
			end;
		}
		set .@master, getcharid(0);
	}

	getmapxy .@m$,.@x,.@y,0;
	do {
		set .@x1,.@x + (rand(10) - 5);
		set .@y1,.@y + (rand(10) - 5);
		if (set(.@i,.@i+1) >= 10) {
			set .@x1,.@x;
			set .@x1,.@x;
			break;
		}
	} while( checkcell(.@m$,.@x1,.@y1,cell_chknopass) );

	if( clone(.@m$,.@x1,.@y1,"",.@id3,.@master,0,.@flag?1:0,0) )
	{
		dispmes msg(128+.@flag*2);
		end;
	}
	dispmes msg(129+.@flag*2);
}

function	script	cmd_disguise	{
	if(getarg(0)&1)
	{
		set .@id2, atoi(@cmd_para$[0]);
		if( existmob(.@id2) ) 
			set .@id, .@id2;
		else if( (.@id2 >= 46 && .@id2 <= 125) || .@id2 == 139 || (.@id2 >= 400 && .@id2 <= MAX_NPC_CLASS) || .@id2 == 32767 ) 
			set .@id, .@id2;
		else if( !set(.@id, existmob(@cmd_para$[0])) ) 
		{ 
			if( set(.@id, getnpcdata(10,@cmd_para$[0])) == -1 )
			{
				dispmes msg(123); 
				end; 
			}
		} 
		if( checkriding() )
		{ 
			dispmes "坐骑时无法伪装。"; 
			end; 
		} 
	}

	if( getarg(0)&2 )
	{
		if(getarg(0)&1)
		{
			set $@id,.@id;
			script4each "{ disguise $@id; }",0;
			dispmes msg(122);
			end;
		}
		script4each "{ undisguise; }",0;
	}
	else
	{
		if(getarg(0)&1)
		{
			disguise .@id;
			dispmes msg(122);
			end;
		}
		if( undisguise() )
		{
			dispmes msg(125);
			end;
		}
	}
	dispmes msg(124);
}

function	script	F_ALLRIDING	{
	switch(BaseJob)
	{
	case Job_Knight:
	case Job_Crusader:
		setriding(checkriding()?0:1);
		break;
	case Job_Blacksmith:
		if(Upper&4)
		{
			setriding(checkriding()?0:1);
			break;
		} //Let it fall through
	case Job_Hunter:
		if(Upper&4)
		{
			if( checkoption(0x00200020) )
			{
				unitskilluseid getcharid(3),"RA_WUGRIDER",1;
				unitskilluseid getcharid(3),"RA_WUGMASTERY",1;	
			}
			else
			{
				setwarg();
				unitskilluseid getcharid(3),"RA_WUGRIDER",1;
			}
			break;
		} //Let it fall through
	default:
		if( sc_check(SC_ALLRIDING) ) 
			sc_end SC_ALLRIDING; 
		else 
			sc_start SC_ALLRIDING,-1,0;
		break;
	}
}

-	script	坟墓::Grave_System	565,{
	set .@i,atoi(strnpcinfo(2));
	mes "[ ^0000FF"+mob_death_info(0,.@i)+"^000000 ]";
	mes "在此结束短暂的一生吧";
	mes "死亡时间 : ^0000FF"+mob_death_info(4,.@i)+":"+mob_death_info(5,.@i)+"^000000";
	mes "将其送上不归之路者";
	mes "[^0000FF"+(mob_death_info(6,.@i)==""?"无名氏":mob_death_info(6,.@i))+"^000000]";
	close;
}

-	script	ViewEquipEvent	-1,{
OnPCViewEquipEvent:
	viewequip @eqview_id;
	end;
}

function	script	delequip	{
	delinventory unequip(getarg(0)),1;
	return;
}

function	script	getrandgroupitem	{
	getitem groupranditem(getarg(0)),getarg(1);
	return;
}

function	script	consumeitem	{
	runitemscript getarg(0);
	return;
}

function	script	isbegin_quest	{
	 switch(checkquest(getarg(0))) 
	{
	 case -1: return 0; // 无任务
	 case 0:
	 case 1:  return 1; // 进行中
	 case 2:  return 0; // 已完成
	 default: return 0; // 异常
	 }
}

function	script	instance_create	{
	if( getargcount() == 2 )
		return instance_create2(getarg(0),getarg(1));
	if( instance_getdbinfo(getarg(0),0) == -1 )
		return -1;
	if( !getcharid(1) )
		return -2;
	set .@instance, instance_create2(getarg(0), getcharid(1));
	if( .@instance == -1 )//party already instancing
		return -3;
	if( .@instance == -2 )//no free instance
		return -4;
	set .@limit, instance_getdbinfo(getarg(0),1);
	instance_set_timeout .@limit,300,.@instance;
	for(.@i=0; .@i < 10 && instance_getdbinfo(getarg(0),5+.@i) != ""; .@i++)
		instance_attachmap(instance_getdbinfo(getarg(0),5+.@i),.@instance);
	instance_init .@instance;
	instance_attach .@instance;
	instance_doevent .@instance;
	return .@instance;
}

function	script	instance_enter	{
	if( instance_getdbinfo(getarg(0),0) == -1 )
		return 3;
	if( !getcharid(1) )
		return 1;
	if( has_instance(instance_getdbinfo(getarg(0),2)) == "" )
		return 2;
	warp has_instance(instance_getdbinfo(getarg(0),2)),instance_getdbinfo(getarg(0),3),instance_getdbinfo(getarg(0),4);
	return 0;
}

function	script	instance_mapname	{
	if(getargcount() >= 2)
		return has_instance(getarg(0),getarg(1));
	else
		return has_instance(getarg(0));
}
 
//&0x001 = invisible
//&0x002 = visible
//&0x004 = hide
//&0x008 = reveal
//&0x010 = disable
//&0x020 = enable
function	script	disablenpc	{
	npc_enable(getarg(0),0x11);
	return;
}
function	script	enablenpc	{
	npc_enable(getarg(0),0x22);
	return;
}
function	script	hideonnpc	{
	npc_enable(getarg(0),0x14);
	return;
}
function	script	hideoffnpc	{
	npc_enable(getarg(0),0x28);
	return;
}
function	script	hideonnpc2	{
	npc_enable(getarg(0),0x4);
	return;
}
function	script	hideoffnpc2	{
	npc_enable(getarg(0),0x8);
	return;
}
/*
-	script	AcidBombHalfDmgonPC	-1,{
OnPCStatCalcEvent:
	bonus bonus2 bSkillDef,"CR_ACIDDEMONSTRATION",50;
	end;
}
*/

function	script	Channel_Ally_Join	{
	channel_create $@ally_chat_nick$+getarg(0),$@channel_pass$,$@ally_chat_color,$@ally_chat_delay;//doesn't matter if create repeatedly.[Inkfish]
	channel_join $@ally_chat_nick$+getarg(0),$@channel_pass$;
	return;
}

-	script	Channel_System	-1,{
OnInterIfInitOnce:
	set $@main_chat_nick$,"广播";
	set $@ally_chat_nick$,"同盟";
	set $@map_chat_nick$,"地图";
	set $@main_chat_color,0xffffff;//白色
	set $@ally_chat_color,0xe57c00;//橘色
	set $@map_chat_color,0x28bf00;//墨绿色
	set $@main_chat_delay,100;
	set $@ally_chat_delay,100;
	set $@map_chat_delay,100;
	set $@channel_pass$,rand(100000,999999);
	//创建世界频道
	channel_create $@main_chat_nick$,"",$@main_chat_color,$@main_chat_delay;
	end;

OnPCLoginEvent:
	//channel_join $@main_chat_nick$,"";	//加入世界频道，默认不加入
OnPCGuildJoinEvent:
	if(getcharid(2) == 0) end;
	getguildallies getcharid(2);
	for( .@i = 0; .@i < $@guildalliescount; .@i++ )
		Channel_Ally_Join($@guildidallies);//加入同盟公会频道。
OnPCGuildCreateEvent:
	Channel_Ally_Join(getcharid(2));//加入自己公会的频道
	channel_bindname($@ally_chat_nick$,$@ally_chat_nick$+getcharid(2));//绑定#同盟为自己公会频道
	end;

OnPCAllyAddEvent:
	Channel_Ally_Join(@allychange_gid);//加入同盟公会频道
	end;
OnPCAllyDelEvent:
	channel_leave $@ally_chat_nick$+@allychange_gid;//离开同盟公会频道
	end;

OnPCGuildLeaveEvent:
OnPCGuildBreakEvent:
	getguildallies getcharid(2);
	for( .@i = 0; .@i < $@guildalliescount; .@i++ )
		channel_leave $@ally_chat_nick$+$@guildidallies;//离开同盟公会频道
	channel_leave $@ally_chat_nick$+getcharid(2);//离开自己公会频道
	end;

//@main 指令的事件
OnMainBind:
	if(!@mainbinded) {
		channel_join $@main_chat_nick$,"";
		channel_bind $@main_chat_nick$;	
		@mainbinded = 1;
		dispmes "开启世界频道模式！"; end;
	}
	else
	{
		channel_unbind;
		@mainbinded = 0;
		dispmes "关闭世界频道模式！"; end;
	}
	end;

//地图频道。请在要使用的地图激活loadmapevent或全部地图激活
OnPCLoadMapEvent:	//使用“#地图名”在地图频道中发言
	channel_leave @premap$;//没加入退出没关系。
	channel_create strcharinfo(3),$@map_chat_pass$,$@map_chat_color,$@map_chat_delay;//重复创建没关系。
	channel_join strcharinfo(3),$@channel_pass$;
	channel_bindname $@map_chat_nick$,strcharinfo(3);
	end;
}

function	script	bonus_DemiHuman	{
	bonus getarg(0),RC_DemiHuman;
	bonus getarg(0),RC_Player;
	return;
}
function	script	bonus2_DemiHuman	{
	bonus2 getarg(0),RC_DemiHuman,getarg(1);
	bonus2 getarg(0),RC_Player,getarg(1);
	return;
}
function	script	bonus3_DemiHuman	{
	bonus3 getarg(0),RC_DemiHuman,getarg(1),getarg(2);
	bonus3 getarg(0),RC_Player,getarg(1),getarg(2);
	return;
}